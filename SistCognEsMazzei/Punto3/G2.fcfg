% start S
############################
# Grammar Rules
#############################
S[SEM = <?subj(?vp)>] -> NP[NUM=?n,SEM=?subj] VP[NUM=?n,SEM=?vp]

NP[NUM=?n,SEM=?np] -> PropN[NUM=?n,SEM=?np]
NP[GEN=?g,NUM=?n,SEM=<?art(?nc)>] -> DT[GEN=?g,NUM=?n,SEM=?art] N[GEN=?g,NUM=?n,SEM=?nc]
NP[NUM=?n,SEM=<?cg(?np)>] -> CG[NUM=?n,SEM=?cg] NP[SEM=?np]
NP[GEN=?g,NUM=?n,SEM=<?tt(?nc)>] -> T[GEN=?g,NUM=?n,SEM=?tt] NP[GEN=?g,NUM=?n,SEM=?nc]

CG[NUM=?n,SEM=<?cc(?np)>] -> NP[SEM=?np] CC[NUM=?n,SEM=?cc]

CC[NUM=?n,SEM=?cc] -> Cong[NUM=?n,SEM=?cc]

N[NUM=?n,SEM=?nc] -> ComN[NUM=?n,SEM=?nc]

#TT[NUM=?n,SEM=<?t(?dt)>] -> T[NUM=?n,SEM=?t] DT[NUM=?n,SEM=?dt]

DT[NUM=?n,SEM=?dt] -> Art[NUM=?n,SEM=?dt]

VP[NUM=?n,SEM=<?v(?obj)>] -> V[NUM=?n,SEM=?v] NP[SEM=?obj]
VP[NUM=?n,SEM=<?vi>] -> VI[NUM=?n,SEM=?vi] 
VP[NUM=?n,SEM=<?vp(?ca)>] -> VP[NUM=?n,SEM=?vp] CA[SEM=?ca]
VP[NUM=?n,SEM=<?vp(?ca)>] -> VP[NUM=?n,SEM=?vp] NP[SEM=?ca]

CA[NUM=?n,SEM=?np] -> PR[NUM=?n] NP[NUM=?n,SEM=?np]


#############################
# Lexical Rules
#############################

PropN[NUM=sg,SEM=<\P.P(paolo)>] -> 'Paolo'
PropN[NUM=sg,SEM=<\P.P(francesca)>] -> 'Francesca'
PropN[NUM=sg,SEM=<\P.P(caronte)>] -> 'Caronte'
PropN[NUM=sg,SEM=<\P.P(dante)>] -> 'Dante'
PropN[NUM=sg,SEM=<\P.P(virgilio)>] -> 'Virgilio'
PropN[NUM=sg,SEM=<\P.P(beatrice)>] -> 'Beatrice'

Cong[NUM=pl,SEM=<\X Y R.(X(R) & Y(R))>] -> 'e'

ComN[GEN=m,NUM=sg,SEM=<\x.man(x)>] -> 'uomo'
ComN[GEN=f,NUM=sg,SEM=<\x.woman(x)>] -> 'donna'
ComN[GEN=m,NUM=sg,SEM=<\x.hell(x)>] -> 'inferno'
ComN[GEN=m,NUM=sg,SEM=<\x.gift(x)>] -> 'dono'
ComN[GEN=f,NUM=pl,SEM=<\x.star(x)>] -> 'stelle'
ComN[GEN=f,NUM=pl,SEM=<\x.woman(x)>] -> 'donne'
ComN[GEN=m,NUM=pl,SEM=<\x.man(x)>] -> 'uomini'

Art[GEN=m,NUM=sg,SEM=<\P Q.(exists x.(P(x) & Q(x)))>] -> 'un'
Art[GEN=f,NUM=pl,SEM=<\P Q.exists x.((P(x) & Q(x)) & all y.(P(y) -> (x=y)))>] -> 'le'
Art[GEN=m,NUM=pl,SEM=<\P Q.exists x.((P(x) & Q(x)) & all y.(P(y) -> (x=y)))>] -> 'gli'
Art[GEN=f,NUM=sg,SEM=<\P Q.exists x.(P(x) & Q(x))>] -> 'una'


V[NUM=sg,SEM=<\X x.X(\y.love(x,y))>] -> 'ama'
V[NUM=sg,SEM=<\X x.X(\y.hate(x,y))>] -> 'odia'
V[NUM=pl,SEM=<\X x.X(\y.dream(x,y))>] -> 'sognano'
V[NUM=sg,SEM=<\X x.X(\y.worship(x,y))>] -> 'adora'
V[NUM=pl,SEM=<\X x.X(\y.visit(x,y))>] -> 'visitano'
V[NUM=pl,SEM=<\Y X x.X(\z.Y(\y.leave(x,y,z)))>] -> 'lasciano'
V[NUM=sg,SEM=<\X x.X(\y.see_again(x,y))>] -> 'rivede'
V[NUM=pl,SEM=<\X x.X(\y.worship(x,y))>] -> 'adorano'

VI[NUM=sg,SEM=<\x. sail(x)>] -> 'naviga'

T[GEN=m,NUM=pl,SEM=<\P Q.all x. (P(x) -> Q(x))>] -> 'tutti'  
T[GEN=f,NUM=pl,SEM=<\P Q.all x. (P(x) -> Q(x))>] -> 'tutte' 

PR[NUM=sg] -> 'a'


